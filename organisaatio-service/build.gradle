plugins {
    id 'org.springframework.boot' version '2.7.17'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id 'java'
    id 'jacoco'
    id 'application'
    id "org.flywaydb.flyway" version "6.5.7"
}
ext {
    queryDslVersion = '5.0.0'
    junitVersion = '5.6.2'
}

flyway {
    url = 'jdbc:postgresql://localhost:5432/organisaatio'
    table = 'schema_version_history'
    user = 'postgres'
    locations = ['classpath:db.migration']
}
// we need to build classes before we can migrate
flywayMigrate.dependsOn classes

compileJava {
    options.annotationProcessorPath = configurations.annotationProcessor
}

dependencies {
    implementation project(':organisaatio-ui')
    implementation project(':organisaatio-api')
    implementation project(':ytj-client:ytj-api')
    implementation(project(':ytj-client:ytj-webservice')) {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'org.slf4j', module: 'log4j'
    }

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-freemarker'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-log4j2'
    implementation group: 'org.springframework.session', name: 'spring-session-jdbc'
    implementation group: 'org.springframework.security', name: 'spring-security-cas', version: '5.3.0.RELEASE'

    implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.6.13'
    implementation group: 'fi.vm.sade.java-utils', name: 'java-http', version: '0.6.1-SNAPSHOT'
    implementation group: 'fi.vm.sade.java-utils', name: 'oid-generator', version: '0.1.0-SNAPSHOT'
    implementation group: 'fi.vm.sade.java-utils', name: 'opintopolku-cas-servlet-filter', version: '0.1.2-SNAPSHOT'
    implementation group: 'fi.vm.sade.java-utils', name: 'opintopolku-user-details-service', version: '0.2.0-SNAPSHOT'
    implementation group: 'fi.vm.sade', name: 'auditlogger', version: '8.3.0-SNAPSHOT'
    implementation group: 'org.jasig.cas.client', name: 'cas-client-core', version: '3.5.0'
    implementation group: 'org.postgresql', name: 'postgresql'
    implementation group: 'com.github.kagkarlsson', name: 'db-scheduler-spring-boot-starter', version: '11.7'
    implementation group: 'org.modelmapper', name: 'modelmapper', version: '3.2.0'
    implementation group: 'org.flywaydb', name: 'flyway-core', version: '6.5.7'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.9'

    implementation(group: 'commons-fileupload', name: 'commons-fileupload', version: '1.4') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'org.slf4j', module: 'log4j'
    }

    implementation(group: 'fi.vm.sade.tarjonta', name: 'tarjonta-api', version: '9.7-SNAPSHOT') {
        exclude group: '*', module: '*'
    }

    // Logback
    implementation group: 'ch.qos.logback', name: 'logback-access', version: '1.2.6'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.9'

    // AWS
    implementation group: 'com.amazonaws', name: 'aws-java-sdk-sns', version: '1.12.328'
    implementation platform('software.amazon.awssdk:bom:2.25.23')
    implementation 'software.amazon.awssdk:sts'
    implementation 'software.amazon.awssdk:s3'
    implementation 'software.amazon.awssdk:s3-transfer-manager'

    compileOnly group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'
    // Lombok
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.30'

    // QueryDSL & JPA
    annotationProcessor group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    implementation group: 'com.querydsl', name: 'querydsl-core', version: queryDslVersion
    implementation group: 'com.querydsl', name: 'querydsl-jpa', version: queryDslVersion
    compileOnly group: 'com.querydsl', name: 'querydsl-jpa', version: queryDslVersion
    annotationProcessor group: 'com.querydsl', name: 'querydsl-apt', version: queryDslVersion, classifier: 'jpa'

    implementation group: 'com.google.guava', name: 'guava', version: '33.1.0-jre'

    // Excel
    implementation 'org.apache.poi:poi:5.2.5'
    implementation 'org.apache.poi:poi-ooxml:5.2.5'

    implementation group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.14'

    // Test Dependencies
    testImplementation group: 'org.springframework.security', name: 'spring-security-test', version: '5.4.1'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitVersion
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation group: 'org.powermock', name: 'powermock-reflect', version: '2.0.7'
    testImplementation group: 'com.github.tomakehurst', name: 'wiremock-jre8', version: '2.29.0'
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
}

application {
    mainClass.set('fi.vm.sade.organisaatio.OrganisaatioApplication')
    applicationDefaultJvmArgs = ['-Dspring.config.location=classpath:application.properties,classpath:application-test-envs.properties',
                                 '-Dspring.profiles.active=dev',
                                 '-Dspring.flyway.enabled=true',
                                 '-Durl-virkailija=http://localhost:9000',
                                 '-Dhost.virkailija=localhost:9000',
                                 '-Durl-ytj=http://localhost:9000/ytj',
                                 '-Durl-oidservice=http://localhost:9000/oidservice',
                                 '-Dcas.service.organisaatio-service=http://localhost:8080/organisaatio-service-not-available']
}

configurations {
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
    }
}

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
    }
    finalizedBy jacocoTestReport
}
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
        csv.required = false
    }
}

bootJar.dependsOn(':organisaatio-ui:webjar')
run.dependsOn(':organisaatio-ui:webjar')
startScripts.dependsOn(':organisaatio-ui:webjar')
compileTestJava.dependsOn(':organisaatio-ui:webjar')