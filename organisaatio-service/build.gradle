plugins {
    id 'org.springframework.boot' version '2.7.5'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id 'java'
    id 'jacoco'
    id 'application'
    id "org.flywaydb.flyway" version "6.5.7"
}
sourceCompatibility = '11'
ext {
    queryDslVersion = '5.0.0'
    junitVersion = '5.6.2'
}

flyway {
    url = 'jdbc:postgresql://localhost:5432/organisaatio'
    table = 'schema_version_history'
    user = 'postgres'
    locations = ['classpath:db.migration']
}
// we need to build classes before we can migrate
flywayMigrate.dependsOn classes

compileJava {
    options.annotationProcessorPath = configurations.annotationProcessor
}

mainClassName = 'fi.vm.sade.organisaatio.OrganisaatioApplication'

dependencies {
    implementation project(':organisaatio-ui')
    implementation project(':organisaatio-api')
    implementation project(':ytj-client:ytj-api')
    implementation(project(':ytj-client:ytj-webservice')) {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'org.slf4j', module: 'log4j'
    }

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-freemarker'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-log4j2'
    implementation group: 'org.springframework.session', name: 'spring-session-jdbc'
    implementation group: 'org.springframework.security', name: 'spring-security-cas', version: '5.3.0.RELEASE'

    implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.6.11'
    implementation group: 'fi.vm.sade.java-utils', name: 'java-http', version: '0.6.1-SNAPSHOT'
    implementation group: 'fi.vm.sade.java-utils', name: 'oid-generator', version: '0.1.0-SNAPSHOT'
    implementation group: 'fi.vm.sade.java-utils', name: 'opintopolku-cas-servlet-filter', version: '0.1.2-SNAPSHOT'
    implementation group: 'fi.vm.sade.java-utils', name: 'opintopolku-user-details-service', version: '0.2.0-SNAPSHOT'
    implementation group: 'fi.vm.sade', name: 'auditlogger', version: '8.3.0-SNAPSHOT'
    implementation group: 'org.jasig.cas.client', name: 'cas-client-core', version: '3.5.0'
    implementation group: 'org.postgresql', name: 'postgresql'
    implementation group: 'com.github.kagkarlsson', name: 'db-scheduler-spring-boot-starter', version: '11.5'
    implementation group: 'org.modelmapper', name: 'modelmapper', version: '2.4.3'
    implementation group: 'org.flywaydb', name: 'flyway-core', version: '6.5.7'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.9'

    implementation group: 'net.sf.dozer', name: 'dozer', version: '5.5.1'

    implementation group: 'fi.vm.sade.generic', name: 'generic-common', version: '9.6'
    // TODO dinosaurukset xssfilter ja AbstractPermissionService joista pitää päästä eroon


    implementation(group: 'commons-fileupload', name: 'commons-fileupload', version: '1.4') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'org.slf4j', module: 'log4j'
    }

    implementation(group: 'fi.vm.sade.viestintapalvelu', name: 'ryhmasahkoposti-api', version: '16.0-SNAPSHOT') {
        exclude group: 'dom4j', module: 'dom4j'
        exclude group: 'org.scala-lang', module: 'scala-compiler'
        exclude group: 'fi.vm.sade.generic', module: 'generic-common'
        exclude group: 'org.apache.geronimo.specs', module: 'geronimo-servlet_3.0_spec'
    }
    implementation(group: 'fi.vm.sade.tarjonta', name: 'tarjonta-api', version: '9.7-SNAPSHOT') {
        exclude group: 'org.apache.geronimo.specs', module: 'geronimo-servlet_2.5_spec'
        exclude group: 'org.apache.cxf', module: 'cxf-bundle'
    }
    implementation(group: 'fi.vm.sade.koodisto', name: 'koodisto-api', version: '9.0-SNAPSHOT') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'org.slf4j', module: 'log4j'
        exclude group: 'fi.vm.sade.generic', module: 'generic-common'
    }
    implementation(group: 'fi.vm.sade.oid', name: 'oid-api', version: '8.0') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'org.slf4j', module: 'log4j'
    }

    // Logback
    implementation group: 'ch.qos.logback', name: 'logback-access', version: '1.2.6'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.9'
    // AWS Sns
    implementation group: 'com.amazonaws', name: 'aws-java-sdk-sns', version: '1.12.328'

    compileOnly group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'
    // Lombok
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.24'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.24'

    // QueryDSL & JPA
    annotationProcessor group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    implementation group: 'com.querydsl', name: 'querydsl-core', version: queryDslVersion
    implementation group: 'com.querydsl', name: 'querydsl-jpa', version: queryDslVersion
    compileOnly group: 'com.querydsl', name: 'querydsl-jpa', version: queryDslVersion
    annotationProcessor group: 'com.querydsl', name: 'querydsl-apt', version: queryDslVersion, classifier: 'jpa'

    // Test Dependencies
    testImplementation group: 'org.springframework.security', name: 'spring-security-test', version: '5.4.1'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitVersion
    testImplementation group: 'org.powermock', name: 'powermock-reflect', version: '2.0.7'
    testImplementation group: 'com.github.tomakehurst', name: 'wiremock-jre8', version: '2.29.0'
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testRuntime group: 'com.h2database', name: 'h2', version: '1.3.148'
}

configurations {
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
    }
}

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
    }
    finalizedBy jacocoTestReport
}
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
        csv.required = false
    }
}
