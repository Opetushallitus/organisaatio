plugins {
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id 'java'
    id 'jacoco'
    id 'application'
}
ext {
    queryDslVersion = '5.1.0'
    junitVersion = '5.6.2'
}

compileJava {
    options.annotationProcessorPath = configurations.annotationProcessor
}

mainClassName = 'fi.vm.sade.organisaatio.OrganisaatioApplication'

dependencies {
    implementation project(':organisaatio-ui')
    implementation project(':organisaatio-api')
    implementation project(':ytj-client:ytj-api')
    implementation(project(':ytj-client:ytj-webservice')) {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'org.slf4j', module: 'log4j'
    }

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-freemarker'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-log4j2'
    implementation group: 'org.springframework.session', name: 'spring-session-jdbc'
    implementation group: 'org.springframework.security', name: 'spring-security-cas'

    implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.5.0'
    implementation group: 'fi.vm.sade.java-utils', name: 'java-http', version: '1.0.0-SNAPSHOT'
    implementation group: 'fi.vm.sade.java-utils', name: 'oid-generator', version: '0.1.0-SNAPSHOT'
    implementation group: 'fi.vm.sade.java-utils', name: 'opintopolku-cas-servlet-filter', version: '1.0.0-SNAPSHOT'
    implementation group: 'fi.vm.sade.java-utils', name: 'opintopolku-user-details-service', version: '0.2.0-SNAPSHOT'
    implementation group: 'fi.vm.sade', name: 'auditlogger', version: '8.3.0-SNAPSHOT'
    implementation group: 'org.postgresql', name: 'postgresql'
    implementation group: 'com.github.kagkarlsson', name: 'db-scheduler-spring-boot-starter', version: '11.5'
    implementation group: 'org.modelmapper', name: 'modelmapper', version: '3.2.0'
    implementation group: 'org.flywaydb', name: 'flyway-core'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.9'

    implementation(group: 'commons-fileupload', name: 'commons-fileupload', version: '1.4') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'org.slf4j', module: 'log4j'
    }

    implementation(group: 'fi.vm.sade.viestintapalvelu', name: 'ryhmasahkoposti-api', version: '16.0-SNAPSHOT') {
        exclude group: '*', module: '*'
    }
    implementation(group: 'fi.vm.sade.tarjonta', name: 'tarjonta-api', version: '9.7-SNAPSHOT') {
        exclude group: '*', module: '*'
    }

    // Logback
    implementation group: 'ch.qos.logback', name: 'logback-access', version: '1.2.6'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.9'
    // AWS Sns
    implementation group: 'com.amazonaws', name: 'aws-java-sdk-sns', version: '1.12.328'

    // Lombok
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.30'

    // QueryDSL & JPA
    annotationProcessor group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    implementation group: 'com.querydsl', name: 'querydsl-jpa', version: queryDslVersion, classifier: 'jakarta'
    annotationProcessor group: 'com.querydsl', name: 'querydsl-apt', version: queryDslVersion, classifier: 'jakarta'

    implementation group: 'com.google.guava', name: 'guava', version: '33.1.0-jre'

    // Excel
    implementation group: 'org.apache.poi', name: 'poi', version: '5.2.5'

    implementation group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.14'

    // Test Dependencies
    testImplementation group: 'org.springframework.security', name: 'spring-security-test'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitVersion
    testImplementation group: 'org.powermock', name: 'powermock-reflect', version: '2.0.7'
    testImplementation group: 'com.github.tomakehurst', name: 'wiremock-jre8', version: '3.0.1'
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
}

application {
    applicationDefaultJvmArgs = ['-Dspring.config.location=classpath:application.properties,classpath:application-test-envs.properties',
                                 '-Dspring.profiles.active=dev',
                                 '-Dspring.flyway.enabled=true',
                                 '-Durl-virkailija=http://localhost:9000',
                                 '-Dhost.virkailija=localhost:9000',
                                 '-Durl-ytj=http://localhost:9000/ytj',
                                 '-Durl-oidservice=http://localhost:9000/oidservice',
                                 '-Dcas.service.organisaatio-service=http://localhost:8080/organisaatio-service-not-available']
}

configurations {
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
        exclude group: 'ch.qos.logback', module: 'logback-classic'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
    }
}

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
    }
    finalizedBy jacocoTestReport
}
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
        csv.required = false
    }
}

bootJar.dependsOn(':organisaatio-ui:webjar')
run.dependsOn(':organisaatio-ui:webjar')
startScripts.dependsOn(':organisaatio-ui:webjar')
compileTestJava.dependsOn(':organisaatio-ui:webjar')