plugins {
	id 'java'
	id 'groovy'
	id "org.sonarqube" version "4.4.1.3373"
}
java {
    sourceCompatibility = '21'
    targetCompatibility = '21'
}
compileJava.options.encoding = 'UTF-8'
def getDate() {
	def date = new Date()
	def formattedDate = date.format(versionDateFormat)
	return formattedDate
}

ext['jakarta-xml-bind.version'] = '3.0.1';
ext['jakarta-xml-ws.version']   = '3.0.1';
ext['jakarta-servlet.version']  = '5.0.0';

allprojects {
	apply plugin: 'maven-publish'
	repositories {
        mavenLocal()
        mavenCentral()
		maven {
			url "https://artifactory.opintopolku.fi/artifactory/oph-sade-snapshot-local"
			mavenContent {
				snapshotsOnly()
			}
		}
		maven {
			url "https://artifactory.opintopolku.fi/artifactory/oph-sade-release-local"
			mavenContent {
				releasesOnly()
			}
		}
		maven {
			name "github"
			url "https://maven.pkg.github.com/Opetushallitus/java-utils"
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
				password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
			}
		}
	}
	publishing {
		repositories {
			maven {
				name = 'sadeRepository'
				credentials(PasswordCredentials)
				if(versionSuffix.endsWith('-SNAPSHOT')) {
					url "https://artifactory.opintopolku.fi/artifactory/oph-sade-snapshot-local"
				} else {
					url "https://artifactory.opintopolku.fi/artifactory/oph-sade-release-local"
				}
			}
		}
	}
}
sonarqube {
	properties {
		property "sonar.projectKey", "Opetushallitus_organisaatio"
		property "sonar.organization", "opetushallitus"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}
